<?xml version="1.0" encoding="UTF-8"?>
<!-- for questions and suggestions: dietuyt@mpi.nl -->
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" version="2.0">
    <xsl:output method="xml" version="1.0" encoding="UTF-8" indent="yes"/>
    
    <!-- identity copy -->
    <xsl:template match="@*|node()">
        <xsl:copy>
            <xsl:apply-templates select="@*|node()"/>
        </xsl:copy>
    </xsl:template>
    
    <!-- throw out the reference to the local copy of the XSD generated by oXygen -->
    <xsl:template match="@xsi:noNamespaceSchemaLocation"/>
    
    <!-- clean up the document node -->
    <xsl:template match="CMD_ComponentSpec">
        <!-- lesson: work bottom-up, you cannot change an attribute of an already created/copied node -->
        <xsl:copy>
            <!-- first copy all attributes (if wanted)-->
            <xsl:apply-templates select="@*"/>
                
            <!-- add reference to the online CMDI schema -->
            <xsl:attribute name="xsi:schemaLocation">http://www.clarin.eu/cmd http://www.clarin.eu/cmd/general-component-schema.xsd</xsl:attribute>   
                
            <!-- add this isProfile attribute (if it's not existing yet) -->
            <xsl:if test="not(@isProfile)">
                <xsl:attribute name="isProfile">false</xsl:attribute>
            </xsl:if>
            
            <!-- now copy all nodes  -->
            <xsl:apply-templates select="node()"/>
                
        </xsl:copy>
        
            
     </xsl:template>


    <xsl:template name="main">
        <xsl:for-each select="collection('file:///.?select=*.xml;recurse=no;on-error=ignore')">
            <xsl:result-document href="{document-uri(.)}.cmdi">
                <xsl:apply-templates select="."/>
            </xsl:result-document>
        </xsl:for-each>
    </xsl:template>

</xsl:stylesheet>
